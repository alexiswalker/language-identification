'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'URLHandler.vbs
' Tested on Windows 7 Ultimate
'
'URL handler for use with Hexchat, downloads images and displays them
' in a separate viewer program
'
'Files are saved to the Temporary Folder (C:\Users\[USER]\AppData\Local\Temp\):
' Either in a folder "hexchat_url_handler_temp" with original file name or
' as a temporary name (xxxyyyzzz.tmp.ext).
'
'May be run separately in a command prompt (cmd.exe) by typing:
' cscript.exe URLHandler.vbs [url]
'E.g.:
' cscript.exe URLHandler.vbs https://www.google.se/images/srpr/logo11w.png
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim useOriginalName
'True  = use original names and save in hexchat_url_handler_temp
'False = use random names
useOriginalName = True

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'An uninitialized or empty ("Array()") validExtensions will ignore the file
' extension, i.e. will allow all extensions.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim validExtensions
'validExtensions = Array()
'validExtensions = Array("jpg", "jpeg", "gif", "png")

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Viewer program to run, will be run in a command shell as:
' command + preArguments + imagefile + postArguments
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim command, preArguments, postArguments
'' Windows Photo Viewer (can also process URLs directly, so not really useful in this script):
'command = "rundll32.exe ""C:\PROGRA~1\Windows Photo Viewer\PhotoViewer.dll,ImageView_Fullscreen"""
'preArguments = ""
'postArguments = ""
'' Irfan View:
command = "C:\PROGRA~1\IrfanView\i_view32.exe"
'preArguments = ""
'postArguments = "/one"


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'EDIT BELOW ON YOUR OWN RISK!
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim fso
Set fso = CreateObject("Scripting.FileSystemObject")

Dim strFileUrl, filename
strFileURL = Wscript.Arguments.Item(0)

tempArr = Split(strFileUrl, "/")
filename = tempArr(UBound(tempArr))
'Remove everything after colon, e.g. twitter's "file.jpg:large"
tempArr = Split(filename, ":")
filename = tempArr(0)

'Wscript.Echo filename

Dim strFileExtension
strFileExtension = ""

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Check if file extension matches valid extensions, continue if empty
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
If Not IsEmpty(validExtensions) Then
 If UBound(validextensions) > 0 Then
  tempArr = Split(filename, ".")
  strFileExtension = LCase(tempArr(ubound(tempArr)))
  Dim found
  found = False
  For Each extension In validExtensions
   If strFileExtension = extension Then
    found = True
    Exit For
   End If
  Next
  If Not found Then
   Wscript.Quit
  End If
 End If
End If

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Get a temporary file name (and folder)
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Dim tfolder, tname
Const TemporaryFolder = 2
Set tfolder = fso.GetSpecialFolder(TemporaryFolder)
If useOriginalName Then
 tname = filename
 tfolder = tfolder & "\" & "hexchat_url_handler_temp"
 If Not fso.FolderExists(tfolder) Then fso.CreateFolder(tfolder)
Else
 tname = fso.GetTempName
 If Len(strFileExtension) > 0 Then
  tname = tname & "." & strFileExtension
 End If
End If
strHDLocation = tFolder & "\" & tname

'Wscript.Echo "strHDLocation: " & strHDLocation
'Wscript.Quit

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Fetch the file
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Set objXMLHTTP = CreateObject("MSXML2.ServerXMLHTTP")
objXMLHTTP.open "GET", strFileURL, false
objXMLHTTP.send()
If objXMLHTTP.Status = 200 Then
 Set objADOStream = CreateObject("ADODB.Stream")
 objADOStream.Open
 objADOStream.Type = 1 'adTypeBinary
 
 objADOStream.Write objXMLHTTP.ResponseBody
 objADOStream.Position = 0
 
 If fso.Fileexists(strHDLocation) Then fso.DeleteFile strHDLocation
 
 objADOStream.SaveToFile strHDLocation
 objADOStream.Close
 Set objADOStream = Nothing
Else
 Wscript.Quit
End If
Set objXMLHTTP = Nothing

Set fso = Nothing

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Run viewer program
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Set oShell = WScript.CreateObject ("WScript.Shell")
oShell.run command & " " & preArguments & " " & strHDLocation & " " & postArguments
Set oShell = Nothing